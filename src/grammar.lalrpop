use std::str::FromStr;
use ast::{SubsExpr, SubsListType, Template};

grammar;

pub SubsExpr: Vec<Box<Template>> = {
    File*
};

File: Box<Template> = {
    "file" <Name> "{" <Matching> "}" => Box::new(Template(<>)),
    "file" r#"""# <Name> r#"""# "{" <Matching> "}" => Box::new(Template(<>)),
};

Matching: Box<SubsListType> = {
    ExprRegular,
    ExprPattern,
};

ExprRegular: Box<SubsListType> = {
    ExprRegularInst+ => Box::new(SubsListType::RegularList(<>))
};

ExprRegularInst: Vec<(String, String)> = {
    "{" <Comma<Makro>> "}" => <>,
};

Makro: (String, String) = {
    <Name> "=" <Name> => (<>),
};

ExprPattern: Box<SubsListType> = {
    <ExprPatternDef> <ExprPatternInst+> => Box::new(SubsListType::PatternList(<>)),
};

ExprPatternDef: Vec<String> = {
    "pattern" "{" <Comma<Name>> "}" => <>,
};

ExprPatternInst: Vec<String> = {
    "{" <Comma<Name>> "}" => <>,
};

Name: String = r"[\p{L}/0-9]+" => <>.to_string();

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

// vim: ft=rust
